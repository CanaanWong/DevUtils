// https://github.com/gradle/gradle/issues
apply plugin: 'maven-publish'
apply plugin: 'signing'

// load properties
Properties properties = new Properties()
File localPropertiesFile = new File(rootDir, "sonatype.properties")
if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
}
File projectPropertiesFile = project.file("project.properties")
if (projectPropertiesFile.exists()) {
    properties.load(projectPropertiesFile.newDataInputStream())
}

// read properties
def projectName = properties.getProperty("project.name")
def projectGroupId = properties.getProperty("project.groupId")
def projectArtifactId = properties.getProperty("project.artifactId")
def projectVersionName = android.defaultConfig.versionName
def projectPackaging = properties.getProperty("project.packaging")
def projectSiteUrl = properties.getProperty("project.siteUrl")
def projectGitUrl = properties.getProperty("project.gitUrl")

def developerId = properties.getProperty("developer.id")
def developerName = properties.getProperty("developer.name")
def developerEmail = properties.getProperty("developer.email")

def signingKeyId = properties.getProperty("signing.keyId")
def signingPassword = properties.getProperty("signing.password")
def signingSecretKeyRingFile = properties.getProperty("signing.secretKeyRingFile")
def ossrhUsername = properties.getProperty("ossrhUsername")
def ossrhPassword = properties.getProperty("ossrhPassword")

def javadocName = properties.getProperty("javadoc.name")

ext["signing.keyId"] = signingKeyId
ext["signing.password"] = signingPassword
ext["signing.secretKeyRingFile"] = signingSecretKeyRingFile
ext["ossrhUsername"] = ossrhUsername
ext["ossrhPassword"] = ossrhPassword

// This generates sources.jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
//    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

// This generates javadoc.jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// javadoc configuration
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version projectVersionName
        links "http://docs.oracle.com/javase/7/docs/api"
        title javadocName
    }
}

// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        release(MavenPublication) {
            groupId projectGroupId
            artifactId projectArtifactId
            version projectVersionName

            // https://docs.gradle.org/6.7.1/dsl/org.gradle.api.publish.maven.MavenPom.html
            pom {
                name = projectName
                url = projectSiteUrl
                packaging = projectPackaging
                groupId = projectGroupId
                artifactId = projectArtifactId
                version = projectVersionName
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = developerId
                        name = developerName
                        email = developerEmail
                    }
                }
                scm {
                    connection = projectGitUrl
                    developerConnection = projectGitUrl
                    url = projectSiteUrl
                }
            }
        }
    }
    repositories {
        maven {
            name = projectName
            def releasesRepoUrl = "https://repo.maven.apache.org/maven2/io/github/afkt"
            def snapshotsRepoUrl = "https://repo.maven.apache.org/maven2/io/github/afkt"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}